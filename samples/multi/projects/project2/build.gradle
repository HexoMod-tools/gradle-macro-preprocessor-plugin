
buildscript {
    repositories {
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath group: 'com.github.hexomod', name: 'MacroPreprocessor', version: '0.9-SNAPSHOT'
        classpath group: 'com.github.hexomod', name: 'ReplaceTokenPreprocessor', version: '0.4-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'application'
}

apply plugin: "com.github.hexomod.macro.preprocessor"
apply plugin: "com.github.hexomod.replace.token.preprocessor"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

mainClassName = 'com.github.hexomod.macro.multi.project2.Main'

// Add api sourceSet
sourceSets {
    api {
        java {
            srcDir '../../core/src/main/java'
        }
        resources {
            srcDir '../../core/src/main/resources'
        }
    }
}

// compile depends on the api sourceSet
dependencies {
    compile sourceSets.api.output
}

// include output of api sourceSet
jar{
    from sourceSets.api.output
}

macroPreprocessorSettings {
    verbose = true      // default: false
    inPlace = false     // default: false
    remove = false      // default: false

    java {
        enable = true       // default: true
        inPlace = true      // default: false
        remove = false      // default: false
    }

    resources {
        enable = true       // default: true
        inPlace = true      // default: true
        remove = true       // default: true
    }

    vars = [VAR_STRING: "value_string", VAR_BOOL: true, VAR_INT: 1, VAR_DOUBLE: 1.5, DEBUG: true]
}

replaceTokenPreprocessorSettings {
    replace '@HELLO@': 'world !'
    replace '@GRADLE@': 'gradle'
    replace '@PROJECT@': 'Project2'
}